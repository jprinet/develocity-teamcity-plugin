plugins {
    id 'groovy'
    id 'io.github.rodm.teamcity-agent'
}

repositories {
    mavenCentral()
}

configurations {
    mvnExtensions {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    mvnExtensions project(path: ':agent:service-message-maven-extension', configuration: 'mvnExtension')
    mvnExtensions files('../custom-develocity-extension/target/convention-develocity-maven-extension-1.0.jar')
    mvnExtensions 'com.gradle:common-custom-user-data-maven-extension:2.0.2'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    testImplementation gradleTestKit()
    testImplementation ('io.ratpack:ratpack-groovy-test:1.10.0-milestone-39') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.19.0'
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0')
    testImplementation('org.spockframework:spock-junit4:2.3-groovy-3.0')
}

def generateInitScript = tasks.register('generateInitScript') {
    def referenceInitScript = file('src/main/resources/init-scripts/develocity-injection.init.gradle')
    def buildScanCollector = file('src/main/resources/init-scripts/build-scan-collector.groovy')
    def generatedInitScript = project.layout.buildDirectory.file('generated-resources/build-scan-init.gradle')
    inputs.file(referenceInitScript)
    inputs.file(buildScanCollector)
    outputs.file(generatedInitScript)
    doLast {
        generatedInitScript.get().asFile.text =
            referenceInitScript.text.replace("class BuildScanCollector {}", buildScanCollector.text)
    }
}

processResources {
    from configurations.mvnExtensions
    from generateInitScript
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        String javaCompilerVersion = findProperty('javaCompilerVersion') ?: '8'
        languageVersion = JavaLanguageVersion.of(javaCompilerVersion)
    }
}

teamcity {
    agent {
        archiveName = 'develocity-teamcity-plugin-agent.zip'
        descriptor {
            pluginDeployment {
                useSeparateClassloader = true
            }
        }
    }
}
